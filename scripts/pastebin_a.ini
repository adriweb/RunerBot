[aliases]
n0=/pastebin {
n1=  ;;;;;;;;; SWITCHES ;;;;;;;;;;
n2=  ; -u  User key
n3=  ; -n  Name
n4=  ; -f  Format
n5=  ; -p  Privacy
n6=  ; -x  Expiration
n7=  ;;;; REQUIRED PARAMETERS ;;;;
n8=  ;     Developer API Key
n9=  ;     File to upload (in quotation marks)
n10=  ;     Operation to call with result
n11=  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
n12=
n13=  tokenize 32 $1-
n14=  var %switches
n15=  var %userkey
n16=  var %name
n17=  var %format
n18=  var %privacy
n19=  var %expiration
n20=  if -* iswm $1 {
n21=    var %switches $right($1,-1)
n22=    tokenize 32 $2-
n23=  }
n24=  while %switches != $null {
n25=    if $1 == $null {
n26=      echo 2 * /pastebin: insufficient parameters
n27=      halt
n28=    }
n29=    var %s $left(%switches,1)
n30=    var %switches $right(%switches,-1)
n31=    if %s == u {
n32=      if $regex($1,/[^0-9A-Fa-f]/) {
n33=        echo 2 * /pastebin: invalid user key $1
n34=        halt
n35=      }
n36=      var %userkey $+(api_user_key=,$1,&)
n37=    }
n38=    elseif %s == n {
n39=      var %name $+(api_paste_name=,$encodeurl($1),&)
n40=    }
n41=    elseif %s == f {
n42=      if !$istok(4cs 6502acme 6502kickass 6502tasm abap actionscript actionscript3 ada algol68 apache applescript apt_sources asm asp autoconf autohotkey autoit avisynth awk bascomavr bash basic4gl bibtex blitzbasic bnf boo bf c c_mac cil csharp cpp cpp-qt c_loadrunner caddcl cadlisp cfdg chaiscript clojure klonec klonecpp cmake cobol coffeescript cfm css cuesheet d dcs delphi oxygene diff div dos dot e ecmascript eiffel email epc erlang fsharp falcon fo f1 fortran freebasic freeswitch gambas gml gdb genero genie gettext go groovy gwbasic haskell hicest hq9plus html4strict html5 icon idl ini inno intercal io j java java5 javascript jquery kixtart latex lb lsl2 lisp llvm locobasic logtalk lolcode lotusformulas lotusscript lscript lua m68k magiksf make mapbasic matlab mirc mmix modula2 modula3 68000devpac mpasm mxml mysql newlisp text nsis oberon2 objeck objc ocaml-brief ocaml pf glsl oobas oracle11 oracle8 oz pascal pawn pcre per perl perl6 php php-brief pic16 pike pixelbender plsql postgresql povray powershell powerbuilder proftpd progress prolog properties providex purebasic pycon python q qbasic rsplus rails rebol reg robots rpmspec ruby gnuplot sas scala scheme scilab sdlbasic smalltalk smarty sql systemverilog tsql tcl teraterm thinbasic typoscript unicon uscript vala vbnet verilog vhdl vim visualprolog vb visualfoxpro whitespace whois winbatch xbasic xml xorg_conf xpp yaml z80 zxbasic,$1,32) {
n43=        echo 2 * /pastebin: invalid format $1
n44=        halt
n45=      }
n46=      var %format $+(api_paste_format=,$lower($1),&)
n47=    }
n48=    elseif %s == p {
n49=      if !$istok(0 1 2,$1,32) {
n50=        echo 2 * /pastebin: invalid privacy boolean $1
n51=        halt
n52=      }
n53=      var %privacy $+(api_paste_private=,$1,&)
n54=    }
n55=    elseif %s == x {
n56=      if !$istok(N 10M 1H 1D 1M,$1,32) {
n57=        echo 2 * /pastebin: invalid expiration $1
n58=        halt
n59=      }
n60=      var %expiration $+(api_paste_expire_date=,$upper($1),&)
n61=    }
n62=    else {
n63=      echo 2 * /pastebin: invalid switch - $+ %s
n64=      halt
n65=    }
n66=    tokenize 32 $2-
n67=  }
n68=  if $2 == $null {
n69=    echo 2 * /pastebin: insufficient parameters
n70=    halt
n71=  }
n72=  if $regex($1,/[^0-9A-Fa-f]/) {
n73=    echo 2 * /pastebin: invalid dev key $1
n74=    halt
n75=  }
n76=  var %devkey $+(api_dev_key=,$1,&)
n77=  tokenize 32 $2-
n78=  if !$regex(pastebin,$1-,/^("[^"]+")/) {
n79=    echo 2 * /pastebin: malformed file path
n80=    halt
n81=  }
n82=  if !$exists($regml(pastebin,1)) {
n83=    echo 2 * /pastebin: no such file $regml(pastebin,1)
n84=    halt
n85=  }
n86=  if $gettok($gettok($1-,2-,$asc(")),1-,32) == $null {
n87=    echo 2 * /pastebin: insufficient parameters
n88=    halt
n89=  }
n90=  var %sockname
n91=  while (%sockname == $null) || ($sock(%sockname) != $null) {
n92=    var %sockname $+(pastebin.,$rand(1,999999999999))
n93=  }
n94=  sockopen %sockname pastebin.com 80
n95=  sockmark %sockname $+(%userkey,%name,%format,%privacy,%expiration,%devkey,api_option=paste&api_paste_code=) $1-
n96=}
n97=/pastebin.return {
n98=  .timer $+ $1 off
n99=  $sock($1).mark $2-
n100=  sockclose $1
n101=}
n102=/encodeurl {
n103=  return $regsubex($1-,/([ !"#$%&'()*,/:;<=>?@[\]^`{|}~ $+ ¡¢£¤¥¦§¨©ª«¬­®¯°±²³´µ¶·¸¹º»¼½¾¿ÀÁÂÃÄÅÆÇÈÉÊËÌÍÎÏÐÑÒÓÔÕÖ×ØÙÚÛÜÝÞßàáâãäåæçèéêëìíîïðñòóôõö÷øùúûüýþÿ])/g,% $+ $base($asc(\1),10,16))
n104=}
